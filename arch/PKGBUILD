# Maintainer: David Herrmann <dh.herrmann@gmail.com>

_pkgorg=dvdhrm
_pkgname=mgmt
pkgdesc='System Management'
pkgver=r6.5b31e74
pkgrel=1

pkgname=dvdhrm-$_pkgname-git
arch=('i686' 'x86_64')
url="https://github.com/$_pkgorg/$_pkgname"
license=('Apache')
provides=("dvdhrm-$_pkgname")
conflicts=("dvdhrm-$_pkgname")

depends=(
  'ack'
  'base'
  'bolt'
  'dialog'
  'dunst'
  'feh'
  'firefox'
  'flatpak'
  'fwupd'
  'git'
  'gnome-icon-theme'
  'gnome-icon-theme-extras'
  'gnome-keyring'
  'gnome-screenshot'
  'htop'
  'libnma'
  'libnotify'
  'lightdm'
  'lightdm-gtk-greeter'
  'man-pages'
  'meson'
  'networkmanager'
  'networkmanager-openvpn'
  'openssh'
  'otf-font-awesome'
  'pavucontrol'
  'perl-authen-sasl'
  'perl-mime-tools'
  'perl-net-smtp-ssl'
  'pipewire'
  'pipewire-pulse'
  'pulseaudio'
  'python-docutils'
  'rustup'
  'sway'
  'swaylock'
  'termite'
  'ttf-dejavu'
  'udisks2'
  'valgrind'
  'vim'
  'vim-editorconfig'
  'waybar'
  'wofi'
  'xdg-desktop-portal-gtk'
  'xdg-desktop-portal-wlr'
  'zathura'
  'zathura-pdf-poppler'
)

makedepends=(
  'git'
  'meson'
)

source=("$pkgname::git+https://github.com/$_pkgorg/$_pkgname")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -Rf build
  mkdir build
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  cp -Tdpr --no-preserve=ownership "../$pkgname/overlay/" "$pkgdir"
}

# vim:set sw=2 et:
